#include "Image.hlsli"
#include "../Shader/Structured.hlsli"
#include "../Shader/Culling.hlsli"

groupshared uint survived_count;
groupshared uint local_indices[32];
groupshared ImageASPayload payload;

[numthreads(32, 1, 1)]
void main(uint3 gtid : SV_GroupThreadID, uint3 dtid : SV_DispatchThreadID)
{
	StructuredBuffer<ImageSpriteInstance> image_sprite = ResourceDescriptorHeap[structured_indices.sprite_.image_index_];
	SceneConstantBuffer scene_constant = GetSceneConstantBuffer();

	if (gtid.x == 0)
	{
		survived_count = 0;
	}
	GroupMemoryBarrierWithGroupSync();

	bool is_visible = false;
	uint sprite_id = dtid.x;

	if (sprite_id < 32768)
	{
		ImageSpriteInstance sprite = image_sprite[sprite_id];

		if (sprite.selected != 0 && sprite.scale.x > 0.0 && sprite.scale.y > 0.0)
		{
			is_visible = IsVisibleInScreen(sprite.position, sprite.texture_size * sprite.scale, scene_constant.screen_size_);
		}
	}

	if (is_visible)
	{
		uint slot;
		InterlockedAdd(survived_count, 1, slot);
		local_indices[slot] = sprite_id;
	}

	GroupMemoryBarrierWithGroupSync();

	if (gtid.x == 0)
	{
		for (uint index = 0; index < survived_count; ++index)
		{
			payload.image_indices[index] = local_indices[index];
		}
	}

	GroupMemoryBarrierWithGroupSync();

	DispatchMesh(survived_count, 1, 1, payload);
}
